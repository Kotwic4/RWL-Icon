procedure main()
  while line:=read() do{
    line := copy(trim_white(line))
    write(line)
    write("line length: ", *line)
    correct(line)
  }
end

procedure trim_white(line)
  return if i:=find(' ',line )  then line[1:i] || trim_white(line[i+1:0])
                                else line
end

procedure correct(line)
  if line?{W()&pos(0)} then write("ok")
  else write("error")
end

# w = s | s^w
# s = c | c|s | c&s
# c = i | i>c
# i = lcase | (w) | ~i | T | F

procedure W()
  suspend S() | S() || ="^" || W()
end

procedure S()
	suspend C() | C() || ="|" || S() | C() || ="&" || S()
end

procedure C()
	suspend I() | I()|| =">" || C()
end

procedure I()
	suspend =!&lcase | ="("||W()|| =")" | ="~"||I() | =!"TF"
end
